plugins {
    id "org.ajoberstar.grgit" version "2.3.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'org.ajoberstar.grgit'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    group = 'company.evo.jmorphy2'
    def libVersion = rootProject.file('project.version')
            .readLines()
            .first()
            .toUpperCase()
    if (project.hasProperty("release")) {
        libVersion = libVersion - "-SNAPSHOT"
    }
    version = libVersion

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked"

    javadoc.options.charSet = 'UTF-8'

    ext {
        defaultEsVersion = rootProject.file('es.version')
                .readLines()
                .first()
        defaultLuceneVersion = '7.7.0'

        esLuceneVersions = [
                '6.6': '7.6.0',
                '6.7': defaultLuceneVersion
        ]
        elasticsearchVersion = project.hasProperty('esVersion') ? project.getProperty('esVersion') : defaultEsVersion
        luceneVersion = esLuceneVersions.get(
                elasticsearchVersion,
                esLuceneVersions.get(elasticsearchVersion[0..2], defaultLuceneVersion)
        )
    }

    repositories {
        mavenCentral()
    }

    test {
        exclude '**/*Benchmark*'
    }
}
