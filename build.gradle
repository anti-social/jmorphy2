subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    group = 'company.evo.jmorphy2'
    version = '0.2.0-SNAPSHOT'

    sourceCompatibility = '1.8'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked"

    javadoc.options.charSet = 'UTF-8'

    ext {
        defaultEsVersion = '6.2.3'
        defaultLuceneVersion = '7.2.1'

        esLuceneVersions = [
                '6.0': '7.0.1',
                '6.1': '7.1.0',
                '6.2': defaultLuceneVersion
        ]
        elasticsearchVersion = project.hasProperty('esVersion') ? project.getProperty('esVersion') : defaultEsVersion
        luceneVersion = esLuceneVersions.get(
                elasticsearchVersion,
                esLuceneVersions.get(elasticsearchVersion[0..2], defaultLuceneVersion)
        )
    }

    repositories {
        mavenCentral()
    }

    test {
        exclude '**/*Benchmark*'
    }
}
